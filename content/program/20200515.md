---
title: "實作Depth of Field"
date: 2020-05-15T11:40:11+08:00
publishdate: 2020-05-15T11:40:11+08:00
tags: ["computer graphics", "computer vision"]
comments: false
---

<script src="https://polyfill.io/v3/polyfill.min.js?features=es6"></script>
<script id="MathJax-script" async src="https://cdn.jsdelivr.net/npm/mathjax@3/es5/tex-mml-chtml.js"></script>


## 介紹
這裡要介紹的是如何快速實作出類似Depth of Field的效果。在上一篇談到了Depth of Field的一些相關理念，在這裡，可以試著去實作。
<!--more-->
Depth of Field基本上影響的因素有焦距、光圈、CoC(模糊圈) 還有物體與鏡頭距離，但如果要直接利用這些物理光學上的條件模擬出來，可能要直接實作類似Ray Tracing的方法。於是在Computer Graphics領域也是有一些相關的方法，
***

### 實作概念

如果要快速實作出一個快速的效果，在這個實作中，有幾個重點

- 需要選定一個Focus Field (也就是對焦點的深度)
- 計算所有Vertex Z Value與Focus Field的深度差
- 利用這個深度差去模擬CoC的值，而用這個值當作Blur強度
- 在此方法是把深度值都記錄在Depth Map上，但其實也可以到了Vertex Shader或Fragment Shader再計算也可以

***

### Fragment Shader Sample Code
主要計算都是在Fragment Shader中實作

```
#version 400

in vec3 Position;
in vec3 Normal;
in vec2 TexCoord;

uniform sampler2D tDiffuse;
uniform vec2 click_coord;

layout( location = 0 ) out vec4 FragColor;

// Normal probability density function, 
// Reference The Normal Distribution.
float normpdf(in float x, in float sigma)
{
	return 0.39894 * exp( -0.5 * x * x / (sigma*sigma)) / sigma;
}

vec4 blurImage(vec2 tex_pos, int blur_size)
{
	int kSize = (blur_size - 1) / 2;
	vec3 final_colour = vec3(0.0);
		
	//create the 1-D kernel
	float sigma = 7.0;
	float Z = 0.0;

	//get the normalization factor (as the gaussian has been clamped)
	for (int j=-kSize; j <= kSize; ++j)
	{ 
		Z += normpdf(float(j), sigma);
	}

	// read out the texels
	for (int i = -kSize; i <= kSize; ++i)
	{
		for (int j = -kSize; j <= kSize; ++j)
		{
			float w = 5.0 / textureSize(tDiffuse, 0).r;
			float h = 5.0 / textureSize(tDiffuse, 0).g;
			vec2 A = tex_pos + vec2(i * w , j * h);
			vec4 packedA = texture( tDiffuse, A );
			
			float kernal_i = normpdf(float(i), sigma);
			float kernal_j = normpdf(float(j), sigma);

			final_colour += kernal_j * kernal_i * packedA.rgb;
		}
	}
	return vec4(final_colour / (Z * Z), 1.0);
}

int computeBlurSize(vec2 tex_pos)
{
	float depth = texture( tDiffuse, tex_pos ).a;
	float focus_field = texture( tDiffuse, click_coord ).a;
	const int max_size = 25;
	const int min_size = 0;
	int blur_size = int(abs(depth - focus_field) * max_size);
	return blur_size;
}

void main() {
	FragColor = blurImage(TexCoord, computeBlurSize(TexCoord));
}


```
<br>

### 講解

- computeBlurSize function : 是用來計算每一個Pixel的深度值和Focus Field的深度值差距，進而推算Blur的強度。
- blurImage function : 根據Input的Blur強度，來決定每個Pixel的Blur狀況。這裡使用的是Gaussian Blur，事實上這部分應該還好更好的Blur方法。
- 把最後的結果帶入 FragColor (Output)

***

### Result
<center>原圖RGB & 深度圖DepthMap
![Result](../20200515/20200515_1.jpg "Result")
<br>
(Left)(Middle)(Right) 分別為不同的對焦點Depth of Field效果
![Result](../20200515/20200515_2.jpg "Result")
</center>
***

### Reference

- Shadertoy https://www.shadertoy.com/view/XdfGDH